set shell := ["bash", "-lc"]

# Default values (override with environment variables)
image_registry := env('IMAGE_REGISTRY', '216989105690.dkr.ecr.ap-northeast-2.amazonaws.com')
image_repo := env('IMAGE_REPO', 'voc-analyst')
image_name := env('IMAGE_NAME', image_registry + "/" + image_repo)
uv := env('UV', 'uv')
aws := env('AWS', 'aws')
aws_profile := env('AWS_PROFILE', 'default')
aws_region := env('AWS_REGION', 'ap-northeast-2')
stack_name := env('STACK_NAME', 'voc-analyst')

job_function_name := env('JOB_FUNCTION_NAME', 'JobFunction')

tag_cache := env('IMAGE_TAG_CACHE', '.last_image_tag')
dockerfile := env('DOCKERFILE_PATH', 'Dockerfile')
build_context := env('BUILD_CONTEXT_PATH', '.')

[doc("Show available recipes")]
default:
  @just --list --unsorted --justfile {{justfile()}}

[doc("Update dependency lockfile with uv")]
lock:
  {{uv}} lock
  cp uv.lock requirements.lock

[doc("Build container image (default tag: vYYYYMMDDHHMMSS)")]
build version="":
  IMAGE_REGISTRY={{image_registry}} IMAGE_REPO={{image_repo}} IMAGE_NAME={{image_name}} IMAGE_TAG={{version}} IMAGE_TAG_CACHE={{tag_cache}} DOCKERFILE_PATH={{dockerfile}} BUILD_CONTEXT_PATH={{build_context}} scripts/build_image.sh {{version}}

[doc("Push to ECR and deploy CloudFormation stack (default tag: latest build)")]
deploy version="":
  AWS_PROFILE={{aws_profile}} IMAGE_REGISTRY={{image_registry}} IMAGE_REPO={{image_repo}} IMAGE_NAME={{image_name}} IMAGE_TAG_CACHE={{tag_cache}} AWS_REGION={{aws_region}} STACK_NAME={{stack_name}} TEMPLATE_FILE=template.yaml AWS_CMD={{aws}} SLACK_BOT_TOKEN="$SLACK_BOT_TOKEN" SLACK_SIGNING_SECRET="$SLACK_SIGNING_SECRET" scripts/deploy_stack.sh {{version}}

[doc("Get deployed Function URL")]
url:
  @AWS_PROFILE={{aws_profile}} AWS_CMD={{aws}} AWS_REGION={{aws_region}} STACK_NAME={{stack_name}} OUTPUT_KEY=WebFunctionUrl scripts/get_stack_output.sh


[doc("Invoke Lambda job with sample payload")]
invoke-job:
  @AWS_PROFILE={{aws_profile}} AWS_CMD={{aws}} AWS_REGION={{aws_region}} STACK_NAME={{stack_name}} JOB_FUNCTION_NAME={{job_function_name}} scripts/invoke_job.sh


[doc("Show currently deployed image/tag/commit info")]
deployed-version:
  @AWS_PROFILE={{aws_profile}} AWS_CMD={{aws}} AWS_REGION={{aws_region}} STACK_NAME={{stack_name}} IMAGE_REPO={{image_repo}} scripts/deployed_version.sh

[doc("Run Litestar dev server locally")]
serve:
  {{uv}} run litestar --app voc_analyst.app:app run --host 0.0.0.0 --port 8080

[doc("Sync LAMBDA_* env vars from .env to template.yaml and deploy_stack.sh")]
sync-env:
  {{uv}} run python scripts/sync_env.py

[doc("Create required IAM roles (run once before first deploy)")]
create-roles:
  AWS_REGION={{aws_region}} AWS_ACCOUNT_ID={{env('AWS_ACCOUNT_ID', '{{ aws_account_id }}')}} STACK_NAME={{stack_name}} AWS_CMD={{aws}} scripts/create_roles.sh
